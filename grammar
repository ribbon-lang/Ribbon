Name = identifier | operator | "`" (identifier | operator) "`"

TypeHead = listSome<identifier (":" Kind)?> ("where" listSome<Type>)?

PartialFixity = "infix" | "prefix" | "postfix" | "atom"
ExactFixity
    = ("infixl" | "infixr" | "infix" | "prefix" | "postfix") sInt?
    | "atom"

Field body = Label body where
    Label = uInt ("\\" Name)? | Name

Path
    = PlainBase ("/" Component++"/")?
    | SlashBase Component**"/"
    | Component++"/" where
    PlainBase = "module" Name | "file" string
    SlashBase = "/" | "./" | "../"+
    Component
        = "namespace" Name
        | PartialFixity? ("type" | "value")? Name

PathExt tail
    = Path.PlainBase ("/" Path.Component++"/")? "/" tail
    | Path.SlashBase (Path.Component++"/" "/" tail | tail)
    | Path.Component++"/" "/" tail
    | tail

WsList sep elem = wsBlock<wsBlock<elem>++(sep?) | elem (sep elem)*>



Module = ModuleHead Doc

ModuleHead = "module" string "@" version wsBlock<Meta> where
    Meta
        = "sources" WsList<",", string>
        | "dependencies" WsList<",", Dependency>
        | identifier WsList<",", string>
    Dependency = string "@" version ("as" Name)?
    Version
        = $uInt "." $uInt "." $uInt
        if $1 > 0 or $2 > 0 or $3 > 0

Doc = Def*

Def
    = Use | Visibility? (Namespace | TypeDef | ValueDef) where
    DefName = ExactFixity? Name
    Use = "use" UseTree where
        UseTree = (Path | PathExt<"{" UseBranch "}" | "..">) ("as" Visibility? Name)?
        UseBranch = UseTree**","

    Namespace = Name "=" "namespace" wsBlock<Doc>

    TypeDef = DefName "=" TypeBody where
        Dec = Name ":" wsBlock<Type>
        ClassDec
            = DefName ":" (("forall" TypeHead "=>")? Type | "type" TypeHead?)
        InstanceDef
            = DefName "=" (wsBlock<Value> | "type" (TypeHead "=>"?) Type)
        TypeBody
            = "type" (TypeHead "=>")? wsBlock<Type>
            | "struct" (TypeHead "=>")? WsList<",", Dec>
            | "union" (TypeHead "=>")? WsList<",", Dec>
            | "effect" (TypeHead "=>")? WsList<",", Dec>
            | "class" (TypeHead "=>")? WsList<",", ClassDec>
            | "instance" TypeHead? "for" Type "=>" WsList<",", InstanceDef>

    ValueDef = DefName ValueBody where
        ValueBody = ValueType? ValueExpr | ValueType
        ValueType = ":" ("forall" TypeHead)? wsBlock<Type>
        ValueExpr = "=" wsBlock<Value>

Kind
    = "type" | "effect"
    | "num" | "str"
    | "data" | "effects"
    | "constraint"
    | Kind "->" Kind
    | "(" Kind ")"

Type |=
    Var = identifier
    Free = "_"
    Con = Path
    Unit = "(" ")"
    Group = "(" Type ")"
    Tuple = "(" Type "," Type**"," ")"
    DataRow = "{" Field<":" Type>**"," "}"
    EffectRow = "[" Type**"," "]"
    Function = Type "->" Type ("in" Type)?
    App = Type Type
    QuantifiedInline = "'" identifier ("of" Kind)?
    Constraint |=
        IsStruct = "struct" Type ("as" Type)?
        IsUnion = "union" Type ("as" Type)?
        HasClass = Type? "with" Type
        HasAssoc = Type? "has" (Name ("~" Type)? | "type" Name)
        RowSub = Type "<" Type?
        RowCat = Type "<>" Type ("~" Type)?
        Equality = Type "~" Type
    User |=
        Infix = Type Path Type
        Prefix = Path Type
        Postfix = Type Path

Value |=
    Var = identifier
    Literal = literal
    Global = Path
    Unit = "(" ")"
    Group = "(" Value ")"
    Tuple = "(" Value "," Value**"," ")"
    Struct = "{" Field<"=" Block>**"," "}"
    AnyUnion = "+/" Name
    Select = Value "." Name
    Concat = Value "<>" Value
    App = Value Block
    Ann = Value ":" wsBlock<Type>
    Function = "fun" listSome<Patt> "=>" Block
    Match = "match" Value wsBlock<Case+>
        where Case = ("|" Patt)+ "=>" Block
    Let = "let" WsList<",", Patt "=" Block>
    Continue = "continue" Block
    Return = "return" Block
    Sequence = Value ";" Value
    Handler = "with" Type "handler" wsBlock<Case+> "do" Block
        where Case
            = Name "|" listSome<Patt> "=>" Block
            | "return" Patt "=>" Block
    User |=
        Infix = Value Path Value
        Prefix = Path Value
        Postfix = Value Path
    where Block = WsList<";", Value>

Patt |=
    Var = identifier
    Literal = literal
    Unit = "(" ")"
    Group = "(" Patt ")"
    Tuple = "(" Patt "," Patt**"," ")"
    Struct = "{" Field<"=" WsBlock<Patt>>**"," ".."? "}"
    AnyUnion = "+/" Name Patt?
    App = Path Patt?
    Alias = Patt "as" identifier
    Ann = Patt ":" wsBlock<Type>
